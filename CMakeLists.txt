CMAKE_MINIMUM_REQUIRED(VERSION 2.8.12 FATAL_ERROR)

INCLUDE(GNUInstallDirs)

# ---[ Project
PROJECT(psimd C)

MACRO(PSIMD_TARGET_ENABLE_C99 target)
  IF(${CMAKE_VERSION} VERSION_LESS "3.1")
    IF(NOT MSVC)
      TARGET_COMPILE_OPTIONS(${target} PRIVATE -std=c99)
    ENDIF()
  ELSE()
    SET_TARGET_PROPERTIES(${target} PROPERTIES
      C_STANDARD 99
      C_EXTENSIONS YES)
  ENDIF()
ENDMACRO()

MACRO(PSIMD_TARGET_ENABLE_CXX11 target)
  IF(${CMAKE_VERSION} VERSION_LESS "3.1")
    IF(NOT MSVC)
      TARGET_COMPILE_OPTIONS(${target} PRIVATE -std=gnu++11)
    ENDIF()
  ELSE()
    SET_TARGET_PROPERTIES(${target} PROPERTIES
      CXX_STANDARD 11
      CXX_STANDARD_REQUIRED YES
      CXX_EXTENSIONS YES)
  ENDIF()
ENDMACRO()

# ---[ Build flags
IF(NOT CMAKE_SYSTEM_PROCESSOR)
  IF(NOT IOS)
    MESSAGE(WARNING "CMAKE_SYSTEM_PROCESSOR is not defined, automatic configuration may choose suboptimal options")
  ENDIF()
ELSEIF(NOT CMAKE_SYSTEM_PROCESSOR MATCHES "^(AMD64|i686|x86_64|armv5te|armv7-a|armv7l|armv7|armv7s|aarch64|arm64|arm64e)$")
  MESSAGE(FATAL_ERROR "Unrecognized CMAKE_SYSTEM_PROCESSOR = ${CMAKE_SYSTEM_PROCESSOR}")
ENDIF()

IF(NOT CMAKE_SYSTEM_NAME)
  MESSAGE(FATAL_ERROR "CMAKE_SYSTEM_NAME not defined")
ELSEIF(NOT CMAKE_SYSTEM_NAME MATCHES "^(Windows|Darwin|Linux|Android)$")
  # ---[ iOS/tvOS/watchOS cross-compilation support begins in CMake 3.14
  IF(${CMAKE_VERSION} VERSION_GREATER_EQUAL "3.14" AND NOT CMAKE_SYSTEM_NAME STREQUAL "iOS")
    MESSAGE(FATAL_ERROR "Unrecognized CMAKE_SYSTEM_NAME = ${CMAKE_SYSTEM_NAME}")
  ENDIF()
ENDIF()

# ---[ Download deps
SET(CONFU_DEPENDENCIES_SOURCE_DIR ${CMAKE_SOURCE_DIR}/deps
  CACHE PATH "Confu-style dependencies source directory")
SET(CONFU_DEPENDENCIES_BINARY_DIR ${CMAKE_BINARY_DIR}/deps
  CACHE PATH "Confu-style dependencies binary directory")

IF(NOT DEFINED VECTORCLASS1_SOURCE_DIR)
  MESSAGE(STATUS "Downloading VectorClass1 to ${CONFU_DEPENDENCIES_SOURCE_DIR}/vectorclass1 (define VECTORCLASS1_SOURCE_DIR to avoid it)")
  CONFIGURE_FILE(cmake/DownloadVectorClass.cmake "${CONFU_DEPENDENCIES_BINARY_DIR}/vectorclass1-download/CMakeLists.txt")
  EXECUTE_PROCESS(COMMAND "${CMAKE_COMMAND}" -G "${CMAKE_GENERATOR}" .
    WORKING_DIRECTORY "${CONFU_DEPENDENCIES_BINARY_DIR}/vectorclass1-download")
  EXECUTE_PROCESS(COMMAND "${CMAKE_COMMAND}" --build .
    WORKING_DIRECTORY "${CONFU_DEPENDENCIES_BINARY_DIR}/vectorclass1-download")
  SET(VECTORCLASS1_SOURCE_DIR "${CONFU_DEPENDENCIES_SOURCE_DIR}/vectorclass1" CACHE STRING "VectorClass1 source directory")
 ENDIF()

# ---[ psimd library
IF(${CMAKE_VERSION} VERSION_LESS "3.0")
  ADD_LIBRARY(psimd STATIC include/psimd.h)
  SET_TARGET_PROPERTIES(psimd PROPERTIES LINKER_LANGUAGE C)
ELSE()
  ADD_LIBRARY(psimd INTERFACE)
ENDIF()
TARGET_INCLUDE_DIRECTORIES(psimd INTERFACE include)

# ---[ Configure VectorClass1
IF(NOT TARGET vectorclass1)
  ADD_SUBDIRECTORY(
    "${VECTORCLASS1_SOURCE_DIR}"
    "${CONFU_DEPENDENCIES_BINARY_DIR}/vectorclass1")
ENDIF()
TARGET_LINK_LIBRARIES(psimd INTERFACE vectorclass1)

INSTALL(FILES include/psimd.h DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})
